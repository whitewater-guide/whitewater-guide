// Top-level build file where you can add configuration options common to all sub-projects/modules.
def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

buildscript {
    ext {
        ndkVersion = "21.4.7075529"
        buildToolsVersion = "30.0.2"
        minSdkVersion = 23
        compileSdkVersion = 31
        targetSdkVersion = 31
        supportLibVersion = "28.0.0"
        kotlinVersion = "1.5.21"
        appCompatVersion = "1.3.1"
        swiperefreshlayoutVersion = "1.1.0"

        // Required by mapbox
        // https://github.com/react-native-mapbox-gl/maps/blob/master/android/install.md#mapbox-maps-sdk
        rnmbglMapboxLibs = {
            implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:9.7.1'
            implementation 'com.mapbox.mapboxsdk:mapbox-sdk-services:5.8.0'
        }
        rnmbglMapboxPlugins = {
            implementation "com.mapbox.mapboxsdk:mapbox-android-gestures:0.7.0"
            implementation "com.mapbox.mapboxsdk:mapbox-android-plugin-localization-v9:0.14.0"
            implementation "com.mapbox.mapboxsdk:mapbox-android-plugin-annotation-v9:0.8.0"
            implementation "com.mapbox.mapboxsdk:mapbox-android-plugin-markerview-v9:0.4.0"
            implementation 'com.mapbox.mapboxsdk:mapbox-sdk-turf:5.8.0'
        }
    }
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:4.2.2")
        classpath 'com.google.gms:google-services:4.3.10'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:1.0.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        maven {
            // All of Detox' artifacts are provided via the npm module
            url "$rootDir/../node_modules/detox/Detox-android"
        }
        // https://github.com/react-native-mapbox-gl/maps/blob/master/android/install.md#mapbox-maps-sdk
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                // Do not change the username below.
                // This should always be `mapbox` (not your username).
                username = 'mapbox'
                // Use the secret token you stored in local.properties as the password
                password = localProperties['MAPBOX_DOWNLOADS_TOKEN'] ?: ""
            }
        }

        google()
        jcenter()
        maven { url 'https://www.jitpack.io' }
    }
}
